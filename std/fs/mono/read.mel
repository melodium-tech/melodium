
use core/fs/read::DirectReading as CoreDirectReading

/**
Read one file.

The content of the file given through `path` is streamed through `data`.

If any reading failure happens, `failure` is emitted and `message` contains text of the related error.
*/
sequence Read()
  input path: Block<string>
  output data: Stream<byte>
  output failure: Block<void>
  output message: Stream<string>
{
    Read: CoreDirectReading()

    Self.path -> Read.path,data -> Self.data
                 Read.failure ---> Self.failure
                 Read.message ---> Self.message
}

use std/filler/scalar/string::Emit

/**
Read one file.

The content of the file at the `path` location is streamed through `data` when `trigger` is enabled.

If any reading failure happens, `failure` is emitted and `message` contains text of the related error.
*/
sequence ReadPath(var path: string)
  input trigger: Block<void>
  output data: Stream<byte>
  output failure: Block<void>
  output message: Stream<string>
{
    Read()
    Emit(value=path)

    Self.trigger -> Emit.trigger,value -> Read.path
    Read.data ----> Self.data
    Read.failure -> Self.failure
    Read.message -> Self.message
}

use core/linearize::LinearizeU16

/**
Linearize stream of `Vec<u16>` into stream of `Scalar<u16>`.

All the input vectors are turned into continuous stream of scalar values, in the same order.
```mermaid
graph LR
    T(Linearize)
    B["［🟦 🟦］［🟦］［🟦 🟦 🟦］"] -->|vector| T
    
    T -->|value| O["🟦 🟦 🟦 🟦 🟦 🟦"]

    style B fill:#ffff,stroke:#ffff
    style O fill:#ffff,stroke:#ffff
```
*/
sequence Linearize()
  input vector: Stream<Vec<u16>>
  output value: Stream<u16>
{
    LinearizeU16()

    Self.vector -> LinearizeU16.vector,value -> Self.value
}


use core/flow::StreamU16ToBlockVecU16

/**
Transform whole stream of `u16` into a single vector block.

All the streamed values are added to a single vector, and once stream is over the vector is transmitted.

```mermaid
graph LR
    T(BlockAllStream)
    B["🟦 🟦 🟦 🟦 🟦 🟦"] -->|stream| T
    
    T -->|block| O["〈［🟦 🟦 🟦 🟦 🟦 🟦］〉"]

    style B fill:#ffff,stroke:#ffff
    style O fill:#ffff,stroke:#ffff
```

⚠️ As the stream values are continuously inserted in vector and so kept in memory, user should be _really careful_ when using this treatment to not saturate memory.

*/
sequence BlockAllStream()
  input  stream: Stream<u16>
  output block:  Block<Vec<u16>>
{
    StreamU16ToBlockVecU16()

    Self.stream -> StreamU16ToBlockVecU16.data,data -> Self.block
}


use core/flow::BlockU16ToStream

/**
Stream a block of `u16`.

The received block is sent as streamed value, then the stream is closed.

```mermaid
graph LR
    T(StreamBlock)
    B["〈🟦〉"] -->|block| T
    
    T -->|stream| O["🟦"]

    style B fill:#ffff,stroke:#ffff
    style O fill:#ffff,stroke:#ffff
```
*/
sequence StreamBlock()
  input  block:  Block<u16>
  output stream: Stream<u16>
{
    BlockU16ToStream()

    Self.block -> BlockU16ToStream.data,data -> Self.stream
}


use core/flow::BlockVecU16ToStream

/**
Stream a vector of `u16`.

The received block is sent as streamed vector, then the stream is closed.

```mermaid
graph LR
    T(StreamVecBlock)
    B["〈［🟦 🟦 🟦 🟦 🟦 🟦］〉"] -->|block| T
    
    T -->|stream| O["［🟦 🟦 🟦 🟦 🟦 🟦］"]

    style B fill:#ffff,stroke:#ffff
    style O fill:#ffff,stroke:#ffff
```
*/
sequence StreamVecBlock()
  input  block:  Block<Vec<u16>>
  output stream: Stream<Vec<u16>>
{
    BlockVecU16ToStream()

    Self.block -> BlockVecU16ToStream.data,data -> Self.stream
}

use core/organize::OrganizeU16

/**
Organize stream of `u16` into stream of `Vec<u16>`.

ℹ️ If some remaining values doesn't fit into the pattern, they are trashed.
If there are not enough values to fit the pattern, uncomplete vector is trashed.

```mermaid
graph LR
    T(Organize)
    A["… 🟨 🟨 🟨 🟨 🟨 🟨"] -->|value| T
    B["[🟦 🟦] [🟦] [🟦 🟦 🟦]"] -->|pattern| T
    
    T -->|values| O["[🟨 🟨] [🟨] [🟨 🟨 🟨]"]

    style A fill:#ffff,stroke:#ffff
    style B fill:#ffff,stroke:#ffff
    style O fill:#ffff,stroke:#ffff
```

*/
sequence Organize()
  input   value: Stream<u16>
  input pattern: Stream<Vec<void>>
  output values: Stream<Vec<u16>>
{
    OrganizeU16()

    Self.value ---> OrganizeU16.value,values -> Self.values
    Self.pattern -> OrganizeU16.pattern
}


use std/flow/void::Trigger as VoidTrigger
use std/conversion/scalar/u16::ToVoid

/**
Trigger on `u16` stream start and finish.

Send `start` when a first value is send through the stream.
Send `finish` when stream is finally over.

```mermaid
graph LR
    T(Trigger)
    B["🔴 … 🟦 🟦 🟦 🟦 🟦 🟦 … 🟢"] -->|value| T
    
    T -->|start| S["〈🟩〉"]
    T -->|finish| F["〈🟥〉"]

    style B fill:#ffff,stroke:#ffff
    style S fill:#ffff,stroke:#ffff
    style F fill:#ffff,stroke:#ffff
```

ℹ️ If the stream never receive any data before being closed, only `finish` will be emitted.
*/
sequence Trigger()
  input  value:  Stream<u16>
  output start:  Block<void>
  output finish: Block<void>
{
    ToVoid()
    VoidTrigger()

    Self.value -> ToVoid.value,iter -> VoidTrigger.iter
    VoidTrigger.start --> Self.start
    VoidTrigger.finish -> Self.finish
}


use std/flow/void::VecTrigger as VoidVecTrigger
use std/conversion/vector/u16::ToVoid as ToVecVoid

/**
Trigger on `Vec<u16>` stream start and finish.

Send `start` when a first vector is send through the stream.
Send `finish` when stream is finally over.

```mermaid
graph LR
    T(VecTrigger)
    B["🔴 …［🟦 🟦 🟦］［🟦 🟦 🟦］［🟦 🟦 🟦］［🟦 🟦 🟦］ … 🟢"] -->|value| T
    
    T -->|start| S["〈🟩〉"]
    T -->|finish| F["〈🟥〉"]

    style B fill:#ffff,stroke:#ffff
    style S fill:#ffff,stroke:#ffff
    style F fill:#ffff,stroke:#ffff
```

ℹ️ If the stream never receive any vector before being closed, only `finish` will be emitted.
*/
sequence VecTrigger()
  input  value:  Stream<Vec<u16>>
  output start:  Block<void>
  output finish: Block<void>
{
    ToVecVoid()
    VoidVecTrigger()

    Self.value -> ToVecVoid.vector,pattern -> VoidVecTrigger.iter
    VoidVecTrigger.start --> Self.start
    VoidVecTrigger.finish -> Self.finish
}


use std/flow/void::Count as VoidCount

/**
Gives count of elements passing through input stream.

This count increment one for each elements within the `value` stream, starting at 1.

```mermaid
graph LR
    T("Count()")
    V["🟦 🟦 🟦 …"] -->|value| T
    
    T -->|count| P["1️⃣ 2️⃣ 3️⃣ …"]

    style V fill:#ffff,stroke:#ffff
    style P fill:#ffff,stroke:#ffff
```
*/
sequence Count()
  input  value: Stream<u16>
  output count: Stream<u128>
{
    ToVoid()
    VoidCount()

    Self.iter ->  ToVoid.value,iter -> VoidCount.iter,count -> Self.count
}


use std/flow/void::VecCount as VoidVecCount

/**
Gives count of elements passing through input stream.

This count increment one for each elements within the `value` stream, starting at 1.

ℹ️ This does not count the number of elements present in each vector, see the `Size` sequence instead.

```mermaid
graph LR
    T("VecCount()")
    V["［🟦 🟦］［🟦］［🟦 🟦 🟦］…"] -->|value| T
    
    T -->|count| P["1️⃣ 2️⃣ 3️⃣ …"]

    style V fill:#ffff,stroke:#ffff
    style P fill:#ffff,stroke:#ffff
```
*/
sequence VecCount()
  input  value: Stream<Vec<u16>>
  output count: Stream<u128>
{
    ToVecVoid()
    VoidVecCount()

    Self.value -> ToVecVoid.vector,pattern -> VoidVecCount.iter,count -> Self.count
}


use std/flow/void::Size as VoidSize

/**
Gives number of elements present in each vector passing through input stream.

For each vector one `size` value is sent, giving the number of elements contained within matching vector.

```mermaid
graph LR
    T("Size()")
    V["…［🟦 🟦］［🟦］［🟦 🟦 🟦］…"] -->|value| T
    
    T -->|size| P["… 2️⃣ 1️⃣ 3️⃣ …"]

    style V fill:#ffff,stroke:#ffff
    style P fill:#ffff,stroke:#ffff
```
*/
sequence Size()
  input  value: Stream<Vec<u16>>
  output size:  Stream<u64>
{
    ToVecVoid()
    VoidSize()

    Self.value -> ToVecVoid.vector,pattern -> VoidSize.pattern,size -> Self.size
}


use std/flow/void::Fit as VoidFit

/**
Creates stream of vectors based on requested sizes.

For each `size` received, a vector with the same number of values is sent through `pattern`.

```mermaid
graph LR
    T("Fit()")
    V["… 🟦 🟦 🟦 🟦 🟦 🟦 …"] -->|value| T
    S["… 2️⃣ 1️⃣ 3️⃣ …"] -->|size| T
    
    T -->|value| P["…［🟦 🟦］［🟦］［🟦 🟦 🟦］…"]

    style V fill:#ffff,stroke:#ffff
    style S fill:#ffff,stroke:#ffff
    style P fill:#ffff,stroke:#ffff
```
*/
sequence Fit()
  input  value: Stream<u16>
  input  size:  Stream<u64>
  output value: Stream<Vec<u16>>
{
    VoidFit()
    Organize()

    Self.size -> VoidFit.size,pattern -> Organize.pattern
    Self.value ------------------------> Organize.value,values -> Self.value
}


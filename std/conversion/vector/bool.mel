use std/flow/bool::Linearize

use core/conversion/vector::BoolToVoid

/**
Convert stream of `Vec<bool>` into `Vec<void>` one.

This conversion is useful to extract pattern from a stream of vectors and work on it.

*/
sequence ToVoid()
  input  vector:  Stream<Vec<bool>>
  output pattern: Stream<Vec<void>>
{
    BoolToVoid()

    Self.vector -> BoolToVoid.vector,pattern -> Self.pattern
}

use std/conversion/scalar/bool::ToByte as ScalToByte
use std/flow/byte::Organize as OrganizeByte

/**
Convert stream of `Vec<bool>` into `Vec<byte>` one.

`Vec<bool>` gets converted into `Vec<byte>`, and the resulting vectors are send through stream in continuity.
*/
sequence ToByte()
  input  value: Stream<Vec<bool>>
  output data:  Stream<Vec<byte>>
{
    ToVoid()
    Linearize()
    ScalToByte()
    OrganizeByte()

    Self.value -> Linearize.vector,value -> ScalToByte.value,data -> OrganizeByte.value,values -> Self.data
    Self.value -> ToVoid.vector,pattern ---------------------------> OrganizeByte.pattern
}

use std/conversion/scalar/bool::ToString as ScalToString
use std/flow/string::Organize as OrganizeString

/**
Convert stream of `Vec<bool>` into `Vec<string>` one.

`Vec<bool>` gets converted into `Vec<string>`, and the resulting vectors are send through stream in continuity.
*/
sequence ToString()
  input  value: Stream<Vec<bool>>
  output value: Stream<Vec<string>>
{
    ToVoid()
    Linearize()
    ScalToString()
    OrganizeString()

    Self.value -> Linearize.vector,value -> ScalToString.value,value -> OrganizeString.value,values -> Self.value
    Self.value -> ToVoid.vector,pattern ------------------------------> OrganizeString.pattern
}


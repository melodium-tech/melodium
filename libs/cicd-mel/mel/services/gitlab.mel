use http/client/util::post
use std/data::Map
use std/data::|entry
use std/data::|map
use std/text/compose::|format
use std/data/string_map::|entry as |string_entry
use std/data/string_map::|map as |string_map
use std/flow::emit
use std/flow::stream
use std/conv::toString
use std/conv::|to_string
use std/text/convert/string::toUtf8
use std/text/convert/string::fromUtf8
use json::|to_json
use json::Json
use json/value::|null
use std/ops/option::|unwrap_or
use std/ops/option::|wrap
use local::StepState
use log/logger::Logger
use log/log::logError
use log/log::logInfos

treatment postGitlabState[logger: Logger](root_url: string, token: string, project_id: string, sha: string, state: StepState, ref: string, pipeline: string, name: string, description: string)
    input trigger: Block<void>
{
    post(
        headers=|wrap<Map>(|map([
            |entry<string>("PRIVATE-TOKEN", token),
            |entry<string>("Content-Type", "application/json")
        ])),
        url=|format("{root_url}/projects/{project_id}/statuses/{sha}",
                |string_map([
                    |string_entry("root_url", root_url),
                    |string_entry("project_id", project_id),
                    |string_entry("sha", sha)
                ])
        )
    )
    logInfos[logger=logger](label="GitLab API")
    post.data -> fromUtf8.encoded,text -> logInfos.message
    logError[logger=logger](label="GitLab API")
    post.error -> logError.message

    emit<Json>(value=|unwrap_or<Json>(|to_json(
        |format(
            "{{
                \"state\": \"{state}\",
                \"ref\": \"{ref}\",
                \"pipeline_id\": \"{pipeline_id}\",
                \"name\": \"{name}\",
                \"description\": \"{description}\"
            }}",
            |string_map([
                    |string_entry("state", |to_string<StepState>(state)),
                    |string_entry("ref", ref),
                    |string_entry("pipeline_id", pipeline),
                    |string_entry("name", name),
                    |string_entry("description", description)
                ])
        )
    ), |null()))
    stream<Json>()
    toString<Json>()
    toUtf8()

    Self.trigger -> emit.trigger,emit -> stream.block,stream -> toString.value,into -> toUtf8.text,encoded -> post.data
}